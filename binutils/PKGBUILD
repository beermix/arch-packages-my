# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.33.1
pkgrel=61
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(x86_64)
url='https://www.gnu.org/software/binutils/'
license=(GPL)
groups=(base-devel)
depends=(glibc zlib)
checkdepends=(bc)
makedepends=(automake autoconf flex bison)
conflicts=(binutils-multilib)
replaces=(binutils-multilib)
options=(staticlibs !distcc !ccache)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz
      binutils-stable-branch.patch
      0001-gas-Add-md_cons_worker.patch
      CVE-2019-17450.patch
      CVE-2019-17451.patch
      0002-gas-Add-md_generic_table_relax_frag.patch
      0003-i386-Align-branches-within-a-fixed-boundary.patch
      0004-i386-Add-mbranches-within-32B-boundaries.patch
      0005-Don-t-add-prefix-to-instructions-with-TLS-relocation.patch)
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)
md5sums=('9406231b7d9dd93731c2d06cefe8aaf1'
         'SKIP')

prepare() {
  mkdir -p binutils-build

  #cd binutils-gdb
  cd binutils-$pkgver

  patch -p1 -i "$srcdir/binutils-stable-branch.patch"
  patch -p1 -i "$srcdir/CVE-2019-17450.patch"
  patch -p1 -i "$srcdir/CVE-2019-17451.patch"
  patch -p1 -i "$srcdir/0003-i386-Align-branches-within-a-fixed-boundary.patch"
  patch -p1 -i "$srcdir/0004-i386-Add-mbranches-within-32B-boundaries.patch"
  patch -p1 -i "$srcdir/0002-gas-Add-md_generic_table_relax_frag.patch"
  patch -p1 -i "$srcdir/0001-gas-Add-md_cons_worker.patch"
  patch -p1 -i "$srcdir/0005-Don-t-add-prefix-to-instructions-with-TLS-relocation.patch"

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd binutils-build

  "$srcdir/binutils-$pkgver/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-targets=x86_64-linux \
    --enable-threads \
    --disable-gdb \
    --disable-werror \
    --enable-silent-rules \
    --with-pic \
    --with-system-zlib

  make configure-host
  make tooldir=/usr
}

check() {
  cd binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  make -k LDFLAGS="" check || true
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
