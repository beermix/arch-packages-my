# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.34
pkgrel=2
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(x86_64)
url='https://www.gnu.org/software/binutils/'
license=(GPL)
groups=(base-devel)
depends=(glibc zlib elfutils)
makedepends=('elfutils' 'automake' 'autoconf' 'flex' 'bison')
conflicts=(binutils-multilib)
replaces=(binutils-multilib)
options=(staticlibs !distcc !ccache)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig}
	nodocs.patch
	binutils-2.22.52.0.4-no-config-h-check.patch
	binutils-2.29-filename-in-error-messages.patch
	binutils-2.29-revert-PLT-elision.patch
	binutils-attach-to-group.patch
	binutils-CVE-2019-1010204.patch
	binutils-do-not-link-with-static-libstdc++.patch
	binutils-export-demangle.h.patch
	binutils-fix-testsuite-failures.patch
	binutils-gold-ignore-discarded-note-relocs.patch
	binutils-gold-mismatched-section-flags.patch
	binutils-nm-lto-plugin.patch
	binutils-readelf-other-sym-info.patch
	binutils-special-sections-in-groups.patch)
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)
md5sums=('664ec3a2df7805ed3464639aaae332d6'
         'SKIP')

prepare() {
  mkdir -p binutils-build

  #cd binutils-gdb
  cd binutils-$pkgver

  patch -p1 -i "$srcdir/binutils-2.22.52.0.4-no-config-h-check.patch"
  patch -p1 -i "$srcdir/binutils-2.29-filename-in-error-messages.patch"
  patch -p1 -i "$srcdir/binutils-2.29-revert-PLT-elision.patch"
  patch -p1 -i "$srcdir/binutils-attach-to-group.patch"
  patch -p1 -i "$srcdir/binutils-CVE-2019-1010204.patch"
  patch -p1 -i "$srcdir/binutils-do-not-link-with-static-libstdc++.patch"
  patch -p1 -i "$srcdir/binutils-export-demangle.h.patch"
  patch -p1 -i "$srcdir/binutils-fix-testsuite-failures.patch"
  patch -p1 -i "$srcdir/binutils-gold-ignore-discarded-note-relocs.patch"
  patch -p1 -i "$srcdir/binutils-gold-mismatched-section-flags.patch"
  patch -p1 -i "$srcdir/binutils-nm-lto-plugin.patch"
  patch -p1 -i "$srcdir/binutils-readelf-other-sym-info.patch"
  patch -p1 -i "$srcdir/binutils-special-sections-in-groups.patch"

  patch -p1 -i "$srcdir/nodocs.patch"

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd binutils-build

  "$srcdir/binutils-$pkgver/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-targets=x86_64-pep \
    --enable-threads \
    --disable-gdb \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make configure-host
  make tooldir=/usr
}

check() {
  cd binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  make -k LDFLAGS="" check || true
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
