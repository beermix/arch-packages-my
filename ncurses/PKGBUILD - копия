# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=ncurses
pkgver=6.0
pkgrel=11
pkgdesc='System V Release 4.0 curses emulation library'
arch=('i686' 'x86_64')
url='http://invisible-island.net/ncurses/ncurses.html'
license=('MIT')
depends=('glibc' 'gcc-libs')
provides=('libncurses++w.so' 'libformw.so' 'libmenuw.so' 'libpanelw.so'
          'libncursesw.so')
source=(http://invisible-mirror.net/archives/ncurses/ncurses-${pkgver/_/-}.tar.gz{,.asc}
         ncurses-100-ncurses-5.6-20080112-urxvt.patch
         ncurses-101-ncurses-5.6-20080628-kbs.patch
         ncurses-102-ncurses-5.9-gcc-5.patch
         ncurses-200-fix_missing_include.patch
         ncurses-900-terminfo.patch)

md5sums=('ee13d052e1ead260d7c28071f46eefb1'
         'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')  # Thomas Dickey

prepare() {
  mkdir -p ncurses-build

  cd ncurses-$pkgver

  patch -p1 -i "$srcdir/ncurses-100-ncurses-5.6-20080112-urxvt.patch"
  patch -p1 -i "$srcdir/ncurses-101-ncurses-5.6-20080628-kbs.patch"
  patch -p1 -i "$srcdir/ncurses-102-ncurses-5.9-gcc-5.patch"
  patch -p1 -i "$srcdir/ncurses-200-fix_missing_include.patch"
  patch -p1 -i "$srcdir/ncurses-900-terminfo.patch"
}

build() {
  cd $pkgname-${pkgver/_/-}

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-pkg-config-libdir=/usr/lib/pkgconfig \
    --with-shared --with-normal --without-debug --without-ada \
    --enable-widec --enable-pc-files --enable-overwrite\
    --enable-ext-colors --enable-ext-mouse --with-ncursesw  --without-manpages  --with-build-cppflags="-D_GNU_SOURCE" \
	--disable-termcap --disable-warnings --with-termlib=tinfo --with-ticlib
  make
}

package() {
  cd $pkgname-${pkgver/_/-}
  make DESTDIR="$pkgdir" install

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    echo "INPUT(-l${lib}w)" > "$pkgdir"/usr/lib/lib${lib}.so
    ln -s ${lib}w.pc "$pkgdir"/usr/lib/pkgconfig/${lib}.pc
  done

  # some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" > "$pkgdir"/usr/lib/libcursesw.so
  ln -s libncurses.so "$pkgdir"/usr/lib/libcurses.so

  # install license, rip it from the readme
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
